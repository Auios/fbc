{{fbdoc item="title" value="ON ERROR"}}----
Error handling statement to set the current error handler

{{fbdoc item="syntax"}}##
	**On [Local] Error Goto** //label//
##
{{fbdoc item="usage"}}##
	**On [Local] Error Goto** //label//
##
{{fbdoc item="param"}}
	##//label//##
		label to jump to when error occurs

{{fbdoc item="desc"}}
	##[[KeyPgOnerror On Error]]## triggers a jump to an error handler when an error occurs. 

	If ##[[KeyPgLocal Local]]## is not used the handler must be in the main part of the module. Using ##[[KeyPgLocal Local]]## allows to have the error handler inside the ##[[KeyPgSub Sub]]##/##[[KeyPgFunction Function]]## where the ##**On Error**## is located.

	##**On Error GOTO 0**## deactivates the current error handler.

	The use of QB-like error handling requires compilation with switch //[[CompilerOpte -e]]//, //[[CompilerOptex -ex]]// or //[[CompilerOptexx -exx]]// activated, otherwise ##**On Error**## is  inactive but consumes CPU time (##**On Error**## can be used within a preprocessor conditional directive block depending on the value of ##[[KeyPgDdfberr __FB_ERR__]]## in order not to penalize the execution speed if no error checking option).

	See [[TblRuntimeErrors Runtime Error Codes]] for a listing of runtime error numbers and their associated meaning.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/error/onerror.bas"}}%%(freebasic)
'' Compile with QB (-lang qb) dialect

'$lang: "qb"

On Error Goto errorhandler
Error 24 '' simulate an error
Print "this message will not be seen"

errorhandler:
Print "Error #"; Err; "!"
End
%%

{{fbdoc item="filename" value="examples/manual/error/onerror2.bas"}}%%(freebasic)
'' compile as: fbc onerror.bas -ex

#lang "fblite"

function hFileExists( filename as string ) as integer static
	dim f as integer

	hFileExists = 0

	on local error goto exitfunction

	f = freefile
	open filename for input as #f
	
	close #f

	hFileExists = -1

exitfunction:
	exit function
end function


	print "File exists (0=false): "; hFileExists( command )

	on error goto errhandler
	error 1234
	print "back from resume next"
	end 0

errhandler:
	print "error number: " + str( err ) + " at line: " + str( erl )
	resume next
%%

{{fbdoc item="lang"}}
	- ON ERROR is supported in any //[[CompilerOptlang -lang]]// dialect.
	- In the //[[CompilerOptlang -lang fb]]// dialect, statements can be used preferably in function form to return an error code: in that case ON ERROR being ineffective but consuming CPU time must be unused.
{{fbdoc item="filename" value="examples/manual/check/KeyPgOnerror_2.bas"}}%%(freebasic)
'' Compile with FB default (-lang fb) dialect 
If Open( "text" For Input As #1 ) <> 0 Then
  Print "Unable to open file"
End If

%%

{{fbdoc item="diff"}}
	- QB has no LOCAL clause and requires the label to be in the main part of the module. 

{{fbdoc item="see"}}
	- ##[[KeyPgError Error]]##
	- ##[[KeyPgLocal Local]]##
	- ##[[KeyPgErr Err]]##
	- [[TblRuntimeErrors Runtime Error Codes]]
	- {{fbdoc item="keyword" value="ProPgErrorHandling|Error Handling"}}

{{fbdoc item="back" value="CatPgError|Error Handling Functions"}}