{{fbdoc item="title" value="CONST (Member)"}}----
Specifies that a member procedure is read only.

{{fbdoc item="syntax"}}##
	[[KeyPgType Type]] //typename//
		[[KeyPgDeclare declare]] **Const** //membertype// //membername// ...
	End Type
##
{{fbdoc item="param"}}
	##//typename//##
		Name of a user defined data type.
	##//membertype//##
		##[[KeyPgMemberSub Sub]]## or ##[[KeyPgMemberFunction Function]]##.
	##//membername// ...##
		Name of the member to declare or define with parameter list or return value following.

{{fbdoc item="desc"}}
	Specifies that the ##//membertype//## immediately to the right of the ##**Const**## qualifier is to be considered as read only.  Read-only (##**Const**##) declarations are a measure of type safety that can be read as 'promises not to change.'  The compiler uses the const declarations to check operations on variables and parameters and generate an error at compile time if their data could potentially change.  There is no runtime overhead for using ##**Const**## qualifiers since all of the checks are made at compile time.

	##**Const**##, when used preceding ##//membertype//## at the beginning of a declaration indicates that the hidden ##[[KeyPgThis This]]## parameter is considered read-only.  The declaration can be read as 'invoking ##//membertype//## promises not to change ##//typename//##', and the compiler will error if the member procedure tries to change any of the data fields, or calls a non-const member procedure.  Member procedures can not be both ##**Const**## and ##[[KeyPgStaticMember Static]]## since static member procedures do not have a hidden ##[[KeyPgThis This]]## parameter.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/udt/const-proc.bas"}}%%(freebasic)
'' Const Member Procedures

type foo
  x as integer
  c as const integer = 0
  declare const sub Inspect1()
  declare const sub Inspect2()
  declare sub Mutate1()
  declare sub Mutate2()
end type

''
sub foo.Mutate1()
  '' we can change non-const data fields
  x = 1

  '' but we still can't change const data
  '' fields, they are promised not to change
  '' c = 1 '' Compile error

end sub

''
sub foo.Mutate2()
  '' we can call const members
  Inspect1()

  '' and non-const members
  Mutate1()

end sub

''
sub foo.Inspect1()
  '' can use data members
  dim y as integer
  y = c + x

  '' but not change them because Inspect1()
  '' is const and promises not to change foo
  '' x = 10 '' Compile error

end sub

''
sub foo.Inspect2()
  '' we can call const members
  Inspect1()

  '' but not non-const members
  '' Mutate1() '' Compile error

end sub
%%

{{fbdoc item="diff"}}
	- New to FreeBASIC

{{fbdoc item="see"}}
	- ##[[KeyPgConst Const]]##
	- ##[[KeyPgConstQualifier Const (Qualifier)]]##
	- ##[[KeyPgDim Dim]]##
	- ##[[KeyPgType Type]]##

{{fbdoc item="back" value="CatPgProcedures|Procedures"}}{{fbdoc item="back" value="CatPgUserDefTypes|User Defined Types"}}