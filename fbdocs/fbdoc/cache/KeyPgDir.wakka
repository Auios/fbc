{{fbdoc item="title" value="DIR"}}----
Returns a file or directory

{{fbdoc item="syntax"}}##
	[[KeyPgDeclare declare]] [[KeyPgFunction function]] **Dir** ( [[KeyPgByref byref]] //file_spec// [[KeyPgAs as]] [[KeyPgString string]] = "", [[KeyPgByval byval]] //attrib_mask// [[KeyPgAs as]] [[KeyPgInteger integer]] = &h21,  [[KeyPgByval byval]] //out_attrib// [[KeyPgAs as]] [[KeyPgInteger integer]] [[KeyPgPtr ptr]] = 0 ) [[KeyPgAs as]] [[KeyPgString string]]
	[[KeyPgDeclare declare]] [[KeyPgFunction function]] **Dir** ( [[KeyPgByval byval]] //out_attrib// [[KeyPgAs as]] [[KeyPgInteger integer]] [[KeyPgPtr ptr]] = 0 ) [[KeyPgAs as]] [[KeyPgString string]]
##
{{fbdoc item="usage"}}##
	//result// = **Dir**( //file_spec//, //attrib_mask//, //out_attrib// )
		##//or//##
	//result// = **Dir**( //out_attrib// )
##
{{fbdoc item="param"}}
	##//file_spec//##
		Optional ##[[KeyPgString string]]## argument specifying the name of the files or directories to be matched.
	##//attrib_mask//##
		Optional ##[[KeyPgInteger integer]]## mask specifying the file attributes of the files or directories to be matched.  
	##//out_attrib//##
		Optional ##[[KeyPgInteger integer]] [[KeyPgPtr ptr]]## which is set to the attributes of the file or directory returned.

{{fbdoc item="ret"}}
	A ##[[KeyPgString string]]## containing the name of the file or directory returned.

{{fbdoc item="desc"}}
	Returns files and/or directories that match //file_spec// and //attrib_mask//. The //file_spec// parameter can include wildcards ("*" and/or "?") and paths.

	The //attrib_mask// optional parameter is a combination of the following flags (each file with any of them set will be matched):
		{{table columns="3" cellpadding="1" cells="Value;Description;Constant;&h01;Read Only;fbReadOnly;&h02;Hidden;fbHidden;&h04;System;fbSystem;&h10;Directory;fbDirectory;&h20;Archive;fbArchive;&h21;Normal;fbNormal"}}
	Constants can be used by including ##"dir.bi"##

	By default //attrib_mask// is set to &h21 (ie.: search for files with possibly the archive or read-only flags set).

	The value of 55 (63 usually works also, but 55 is safer) returns all files and directories, incl. "." and "..", but no Volume, the value 8 returns the Volume, even if current directory is not the main directory. 

	The //out_attrib// optional parameter if passed will be loaded with the attributes of the file been returned.

	When //file_spec// is set to an empty string or no arguments are passed (or just //file_attrib//), then ##**Dir**## will return the next file found.

	The result will be an empty string when no files were found.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/system/dir.bas"}}%%(freebasic)
#include "dir.bi" 'provides constants to use for the attrib_mask parameter

Sub list_files (byref filespec as string, byval attrib as integer)
	Dim filename As String

	filename = dir( filespec, attrib )
	Do
	    Print filename
	    filename = dir( )
	Loop While len( filename ) > 0
End Sub

Print "directories:"
list_files "*", fbDirectory

Print
Print "archive files:"
list_files "*", fbArchive
%%

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/system/dirretat.bas"}}%%(freebasic)
'' Example of using DIR function and retrieving attributes

#include "dir.bi" '' provides constants to match the attributes against

Dim As Integer out_attr '' integer to hold retrieved attributes (must currently be a signed int)

Dim As String fname '' file/directory name returned with 
dim as integer filecount, dircount

fname = Dir("*.*", 55, @out_attr) '' 55 - Input ATTR mask - all files and subdirs

Print "File listing in " & curdir & ":"

Do until len(fname) = 0 '' loop until Dir returns empty string
	
	If (fname <> ".") And (fname <> "..") Then '' ignore current and parent directory entries
	    
	    print fname,
	    
	    if out_attr and fbDirectory then
	        print "- directory";
	        dircount += 1
	    else
	        print "- file";
	        filecount += 1
	    end if
	    if out_attr and fbReadOnly then print ", read-only";
	    if out_attr and fbHidden then print ", hidden";
	    if out_attr and fbSystem then print ", system";
	    if out_attr and fbArchive then print ", archived";
	    print
	    
	End If
	
	fname = Dir(@out_attr) '' find next name
	
Loop

print
print "Found " & filecount & " files and " & dircount & " subdirs"
%%

{{fbdoc item="target"}}
	- Linux requires the //filename// case matches the real name of the file. Windows and DOS are case insensitive. 
	- Path separators in Linux are forward slashes / . Windows uses backward slashes \ but it allows for forward slashes .  DOS uses backward  \ slashes. 

{{fbdoc item="lang"}}
	- Not available in the //[[CompilerOptlang -lang qb]]// dialect.

{{fbdoc item="diff"}}
	- The //attrib_mask// and //out_attrib// parameters are new to FreeBASIC

{{fbdoc item="see"}}
	- ##[[KeyPgOpen Open]]##
	- ##[[KeyPgCurdir Curdir]]##
	- ##[[KeyPgMkdir Mkdir]]##
	- ##[[KeyPgRmdir Rmdir]]##

{{fbdoc item="back" value="CatPgOpsys|Operating System Functions"}}