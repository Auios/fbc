{{fbdoc item="title" value="IMAGECONVERTROW"}}----
Converts a row of image data into another color depth

{{fbdoc item="syntax"}}##
	[[KeyPgDeclare declare]] [[KeyPgSub sub]] **""ImageConvertRow""** ( [[KeyPgByval byval]] //src// [[KeyPgAs as]] [[KeyPgAny any]]  [[KeyPgPtr ptr]], [[KeyPgByval byval]] //src_bpp// [[KeyPgAs as]] [[KeyPgInteger integer]], [[KeyPgByval byval]] //dst// [[KeyPgAs as]] [[KeyPgAny any]]  [[KeyPgPtr ptr]], [[KeyPgByval byval]] //dst_bpp// [[KeyPgAs as]] [[KeyPgInteger integer]], [[KeyPgByval byval]] //width// [[KeyPgAs as]] [[KeyPgInteger integer]], [[KeyPgByval byval]] //isrgb// [[KeyPgAs as]] [[KeyPgInteger integer]] = 1 )
##
{{fbdoc item="usage"}}##
	**""ImageConvertRow""**( //src//, //src_bpp//, //dst//, //dst_bpp//, //width// )
		##//or//##
	**""ImageConvertRow""**( //src//, //src_bpp//, //dst//, //dst_bpp//, //width//, //isrgb// )
##
{{fbdoc item="param"}}
	##//src//##
		The address of the start of the source row.  The source can either be a full-color image with a bit depth of 24 or 32 bits per pixel, or a paletted image with a bit depth of 1-8 bits per pixel.  Converting a paletted image will only work properly if you are in a screen mode that is using the correct palette for the image when you do the conversion.
	##//src_bpp//##
		The number of bits per pixel in the source row.  1-8, 24 and 32.
	##//dst//##
		The address of the start of the destination row.  The image can be a full-color image of 16 or 32 bits per pixel.  If the source is a paletted image, the destination can also be a paletted image of 1 to 8 bits per pixel.
	##//dst_bpp//##
		The number of bits per pixel in the destination row.  Valid values for this are 1-8, 16 and 32.
	##//width//##
		The length of the row in pixels.
	##//isrgb//##
		A value of zero indicates that the Red and Blue channels are the other way round in the source image.  Use this switch if you want the Red and Blue channels to be swapped in the conversion.

{{fbdoc item="desc"}}
	Copies the row of an image from one memory location to another, converting the color information in each pixel to match the destination image.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/imageconvertrow.bas"}}%%(freebasic)
const as integer w = 64, h = 64
dim as any ptr img8, img32
dim as integer i, y

'' create a 32-bit image, size w*h:
screenres 320, 200, 32, , -1
img32 = imagecreate(w, h)

'' create an 8-bit image, size w*h:
screenres 320, 200, 8, , -1
img8 = imagecreate(w, h)

'' spatter some random dots into the image:
for i = 1 to 1000
	pset img8, ( int(rnd * w), int(rnd * h) ), int(rnd * 256)
next i

'' open a graphics window in 8-bit mode, and PUT the image into it:
screenres 320, 200, 8
windowtitle "8-bit color mode"
put (10, 10), img8

sleep

'' copy the image data into a 32-bit image
for y = 0 to h - 1
	imageconvertrow(img8  + (4 + y * w    ),  8, _
	                img32 + (4 + y * w * 4), 32, _
	                w)
next y

'' open a graphics window in 32-bit mode and PUT the image into it:
screenres 320, 200, 32
windowtitle "32-bit color mode"
put (10, 10), img32

sleep

'' remove the images from memory:
imagedestroy img8
imagedestroy img32
%%

{{fbdoc item="diff"}}
	- New to FB

{{fbdoc item="see"}}
	- ##[[KeyPgScreenres ScreenRes]]##
	- ##[[KeyPgGetgraphics Get (Graphics)]]##
	- ##[[KeyPgPutgraphics Put (Graphics)]]##
	- ##[[KeyPgImagecreate ImageCreate]]##
	- ##[[KeyPgImageDestroy ImageDestroy]]##

{{fbdoc item="back" value="CatPgGfx2D|2D Drawing Functions"}}