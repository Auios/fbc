{{fbdoc item="title" value="DATA"}}----
Statement to store data at compile time

{{fbdoc item="syntax"}}##
	**Data** //constant_expression1// [,//constant_expression2//]...
##
{{fbdoc item="desc"}}
	##Data## stores a list of constant numeric or alphabetical expressions that are evaluated at compile time and stored as constants that can be read into variables by using ##[[KeyPgRead Read]]##. 
	
	All the ##Data## statements in the program behave as a single chained list; after the last element of a ##Data## is read, the first element of the following ##Data## will be read. If a ##Read## is attempted after the last element of the last ##Data##, an error will occur.  

	##Data## statements are only visible from within the module in which they are defined.
	
	##Data## constants can only be of simple types (numeric or string). String constants must be enclosed in double quotes. 
	
	A numeric value can be read as a numeric literal into a string. A string read into a numeric variable will be evaluated by the ##[[KeyPgVal Val]]## function.
	
	##[[KeyPgRestore Restore]]## //label// makes the first  ##Data## item after the //label// the next item to be read, thus altering the order in which the items are ##Read##.

	##Data## is normally used to initialize variables. FreeBASIC also allows the initialization of static variables in their declarations; see [[ProPgInitialization Variable Initializers]] for more information.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/data/data.bas"}}%%(freebasic)
' Create an array of 5 integers and a string to hold the data.
DIM h(4) AS INTEGER
DIM hs AS STRING
dim read_data as integer

' Set up to loop 5 times (for 5 numbers... check the data)
FOR read_data = 0 TO 4

  ' Read in an integer.
  READ h(read_data)

  ' Display it.
  PRINT "Number"; read_data;" = "; h(read_data)

NEXT

' Spacer.
PRINT

' Read in a string.
READ hs

' Print it.
PRINT  "String = " + hs

' Await a keypress.
SLEEP

' Exit program.
END

' Block of data.

DATA 3, 234, 435/4, 23+433, 87643,"Good"+ "Bye!"
%%

{{fbdoc item="lang"}}
	- //[[ProPgCompOptlang -lang fb]]// and //[[ProPgCompOptlang -lang deprecated]]// considers  data items as constant expressions that are evaluated during compilation and its result stored in the program.
	- //[[ProPgCompOptlang -lang qb]]// considers data items as literals and stores then without change, as in QB.  

{{fbdoc item="diff"}}
	- Alphabetic string literals now must be enclosed within quotation marks.
	- Empty items were evaluated in QB to number 0 or to empty strings. In FreeBASIC they give a compile error. A comma after the last item of a line of data makes an empty item (between the comma and the carry return) evaluated to 0 in QB. In FreeBASIC that's an error.

{{fbdoc item="see"}}
	- ##[[KeyPgRead Read]]##
	- ##[[KeyPgRestore Restore]]##

{{fbdoc item="back" value="CatPgMisc|Miscellaneous"}}