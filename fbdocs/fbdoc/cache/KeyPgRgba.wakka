{{fbdoc item="title" value="RGBA"}}----
Computes a valid color value including alpha (transparency) for hi/truecolor modes

{{fbdoc item="syntax"}}##
	[[KeyPgPpdefine #define]] **RGBA**(//r//,//g//,//b//,//a//) (([[KeyPgCuint cuint]](r) [[KeyPgOpShiftLeft shl]] 16) [[KeyPgOpOr or]] ([[KeyPgCuint cuint]](g) [[KeyPgOpShiftLeft shl]] 8) [[KeyPgOpOr or]] [[KeyPgCuint cuint]](b) [[KeyPgOpOr or]] ([[KeyPgCuint cuint]](a) [[KeyPgOpShiftLeft shl]] 24))
##
{{fbdoc item="usage"}}##
	//result// = **RGBA**(//red//, //green//, //blue//, //alpha//)
##
{{fbdoc item="param"}}
	##//red//##
		red color component value
	##//green//##
		green color component value
	##//blue//##
		blue color component value
	##//alpha//##
		alpha component value

{{fbdoc item="ret"}}
	the combined color

{{fbdoc item="desc"}}
	##//red//##, ##//green//##, ##//blue//## and ##//alpha//## are components ranging 0-255.

	The ##**RGBA**## function can be used to compute a valid color value including an alpha channel for use while in hi/truecolor modes. It returns an unsigned integer  in the format ##&hAARRGGBB##, where ##RR##, ##GG##, ##BB##, ##AA##  equal the values passed to this function, in hexadecimal format.

{{fbdoc item="ex"}}
{{fbdoc item="filename" value="examples/manual/gfx/rgba.bas"}}%%(freebasic)
screenres 640,480,32

dim as any ptr img
dim as integer x,y

'make an image that varies in transparency and color
img = imagecreate(128,128,0)
for x = 0 to 127
  for y = 0 to 127
	pset img,(x,y),rgba(x*2,0,y*2,x+y)
  next y
next x
circle img,(64,64),50,rgba(0,127,192,192),,,,f
line img,(50,40)-(78,88),rgba(255,255,255,0),bf

'draw a background
for x=-480 to 639 step 20
  line (x,0)-(x+480,480),rgb(255,255,255)
next
line (10,10)-(630,86),rgb(127,0,0),bf
line (10,290)-(630,438),rgb(0,127,0),bf

'draw the image and some text with PSET
draw string(96,64),"PUT AN IMAGE WITH PSET"
put(96,96),img,pset
put(96,300),img,pset

'draw the image and some text with ALPHA
draw string (416,64),"PUT AN IMAGE WITH ALPHA"

put(416,96),img,alpha
put(416,300),img,alpha

sleep

imagedestroy img
%%

{{fbdoc item="lang"}}
	- Not available in the //[[CompilerOptlang -lang qb]]// dialect.

{{fbdoc item="diff"}}
	- New to FreeBASIC

{{fbdoc item="see"}}
	- ##[[KeyPgColor Color]]##
	- ##[[KeyPgRgb RGB]]##

{{fbdoc item="back" value="CatPgGfx2D|2D Drawing Functions"}}