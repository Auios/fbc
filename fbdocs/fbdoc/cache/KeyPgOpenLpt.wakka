{{fbdoc item="title" value="OPEN LPT"}}----
Open a printer device

{{fbdoc item="syntax"}}##
	**Open Lpt** ["[LPT[x]:][//Printer_Name//][,//TITLE=Doc_Title//][,//EMU=TTY//]"] [FOR INPUT|OUTPUT] AS #handle
##
{{fbdoc item="param"}}
	##//x//##
		Specifies a port number.  If omitted, output is sent to the system print spooler.
	##//Printer_Name//##
		Name of printer to open.  This parameter is ignored on DOS.
	##//TITLE=Doc_Title//##
		Title of the print job as seen by the printer spooler.  This parameter is ignored on DOS.
	##//EMU=TTY//##
		Emulation of TTY output on a windows GDI printer, using driver text imaging.  This parameter is ignored on DOS and Linux.
	##FOR INPUT|OUTPUT##
		clause is allowed for compatibility, but it is ignored.
	##//handle//##
		An unused file handle to assign to the device.

{{fbdoc item="desc"}}
	**Open Lpt** opens a connection to a printer device.  The connection is treated like a file, so data may be written to the printer using ##[[KeyPgPrint Print]]## and ##[[KeyPgPutfileio Put #]]## commands.  

	Any printer attached to the system may be opened with **Open Lpt**

	**Open Lpt** "LPT:" ... will try to open the default printer on Windows and Linux, and "LPT1:" on DOS.

	##[[KeyPgLprint Lprint]]## will automatically try to open the default printer on Windows and Linux, and "LPT1:" on DOS.

Platform specific notes:

	==Windows==
		The argument EMU=TTY assumes printable ASCII or Unicode text, and applies printer driver text imaging to the input.  EMU=TTY also allows the usage of CR, LF, BS, TAB, FF, etc., for virtual print-head movement...even when the printer is a GDI printer and therefore doesn't itself understand these special characters.  If ",EMU=TTY" is omited, the data must be sent in the printer's language (ESC/P, HPGL, PostScript, etc...).  Other useful emulation modes aren't supported yet.

	==Linux==
		A printer spooler available through 'lp' must be installed to access printers by name or a default printer.  Spooler access was tested only with CUPS, but other spoolers may work that are invoked through 'lp'.  Port are zero based on linux.  "LPT1:"  corresponds with "/dev/lp0"

		The data must be sent in the printer's language (ESC/P, HPGL, PostScript, etc...).  Emulation modes aren't supported yet.

	==DOS==
		FreeBASIC does not support print spoolers on DOS.  Printers must be accessable through "LPTx:".

		The data must be sent in the printer's language (ESC/P, HPGL, PostScript, etc...).  Emulation modes aren't supported yet.


{{fbdoc item="ex"}}
%%(qbasic)
' Send some text to the Windows printer on LPT1:, using driver text imaging.
Open LPT "LPT1:EMU=TTY" For Output  As #1
Print #1, "Testing!" 
Close
%%
%%(qbasic)
' Sends contents of text file test.txt to Windows printer named "ReceiptPrinter"
Dim RptInput As String
Dim Printhandle as integer, RptFilehandle as integer

RptFilehandle = Freefile
Open "test.txt" For Input As #RptFilehandle

Printhandle = Freefile
OPEN LPT "LPT:ReceiptPrinter,TITLE=ReceiptWinTitle,EMU=TTY" AS _
	#Printhandle

While (EOF(RptFilehandle) = 0)
	    Line Input #RptFilehandle, RptInput
	    Print #Printhandle, RptInput
Wend

Close #Printhandle  ' Interestingly, does not require CHR(12).  But if pagination is desired, CHR(12) is the way.

Close #RptFilehandle

Print "Press any key to end program..."
GetKey

End
%%

{{fbdoc item="diff"}}
	- In QB the syntax was OPEN "LPT"    or OPEN "PRN"

{{fbdoc item="see"}}
	- ##[[KeyPgLprint Lprint]]##

{{fbdoc item="back" value="CatPgFile|File I/O Functions"}}