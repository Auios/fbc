{{fbdoc item="title" value="DESTRUCTOR"}}----
Called automatically when a class or user defined type goes out of scope or is destroyed

{{fbdoc item="syntax"}}##
	[[KeyPgType Type]] //typename//
		//field declarations//
		[[KeyPgDeclare Declare]] **Destructor** ( )
	End Type
	
	**Destructor** //typename// ( )
		//statements//
	**End Destructor**
##
{{fbdoc item="desc"}}
	The destructor method is called when a user defined ##[[KeyPgType]]## or CLASS variable goes out of scope or is destroyed.

	The ##**Destructor**## method is passed a hidden ##//this//## parameter having the same type as //typename//.

	The destructor on a type is called before the destructors on any of its fields.  Therefore, all fields are accessible with the hidden //this// parameter in the destructor body.

	Only one destructor may be declared and defined per type.

{{fbdoc item="ex"}}
%%(qbasic)
type T
  value as zstring * 32
  declare constructor ( init_value as string )
  declare destructor ()
end type

constructor T ( init_value as string )
  value = init_value
  print "Creating: "; value
end constructor

destructor T ()
  print "Destroying: "; value
end destructor

sub MySub
  dim x as T = ("A.x")
end sub

dim x as T = ("main.x")

scope
  dim x as T = ("main.scope.x")
end scope

MySub
%%
Output:
%%
Creating: main.x
Creating: main.scope.x
Destroying: main.scope.x
Creating: A.x
Destroying: A.x
Destroying: main.x
%%
{{fbdoc item="diff"}}
	- New to FreeBASIC

{{fbdoc item="see"}}
	- ##[[KeyPgConstructor Constructor]]##
	- ##[[KeyPgModuleDestructor Destructor (Module)]]##
	- ##[[KeyPgNamespace Namespace]]##
	- ##[[KeyPgType Type]]##

{{fbdoc item="back" value="CatPgCompound|Compound Statements"}}
