{{fbdoc item="title" value="DIM"}}----
Declares a variable

{{fbdoc item="syntax"}}
	Declaring a Single Variable:
		##**Dim** [[[KeyPgShared Shared]]] //symbolname// [As [[DataType DataType]]] [, ...]##
		##**Dim** [[[KeyPgShared Shared]]] As [[DataType DataType]] //symbolname// [, ...]##

	Declaring Arrays:
		##**Dim** [[[KeyPgShared Shared]]] //symbolname// ( [//lbound// To] //ubound// [, ...] ) [As [[DataType DataType]]] [,...]##
		##**Dim** [[[KeyPgShared Shared]]] As [[DataType DataType]] //symbolname// ( [//lbound// To] //ubound// [, ...] ) [,...]##
	
	Initializing Values:
		##**Dim** //scalar_symbol// As [[DataType DataType]]] = //expression// | [[KeyPgAny Any]]##
		##**Dim** //array_symbol// ([//lbound// To] //ubound//) [AS [[DataType DataType]]] => { //expression// [, ...] } | [[KeyPgAny Any]]##
		##**Dim** //udt_symbol// As [[DataType DataType]] = ( //expression// [, ...] ) | [[KeyPgAny Any]]##

{{fbdoc item="desc"}}
	Declares a named variable and allocates memory to accommodate it.  Though FreeBASIC does not require the declaration of variables, ##**Dim**## is used assign array and variables of any of the supported variable types, UDTs, or enumerations.

	**Declaring Multiple Variables**
		##**Dim**## is a special keyword and supports a number of different syntaxes.  When declaring multiple variables, the first syntax specified above allows multiple variables of multiple types to be declared in one statement.  However, for compactness, the second syntax shown is the preferred method for declaring multiple variables of the same type.  Using the first syntax, the ##As //type//## portion refers only to the variable of that clause.  For instance:
		
			%%(qbasic)Dim var1, var2, var3 As Double    '' RISKY%%	
		This code will declare **only** var3 as a double-precision floating point: //var1// and //var2// will be set to the latest ##**Def""###""**## used, ##[[KeyPgDefint DefInt]]## by default, because no type was specified in their respective clauses.  The same example using the alternative syntax:
	
			%%(qbasic)Dim As Double var1, var2, var3%%	
		Will declare and allocate var1, var2, var3 all as ##[[KeyPgDouble Double]]##s.

	**Arrays**
		As with most BASIC dialects, FreeBASIC supports arrays with indexes ranging from a lower bound to an upper bound.  In the syntaxes shown above, lbound refers to the lower bound, or the smallest index.  Ubound refers to the upper bound, or the largest index.  If a lower bound is not specified, lbound is automatically assumed to be zero, unless ##[[KeyPgOptionbase Option Base]]## is used,  so an array declared with the following code:
	
			%%(qbasic)Dim array(upperbound) As type%%
		Will declare an array with indexes ranging from 0 to ##//upperbound//##, for a total of (upperbound + 1) indexes.  

		Multidimensional arrays can be declared as well.  The following example will declare a three-dimensional array of single precision floating-point numbers. The maximum number of dimensions of a multidimensional array is 8. 
		Multidimensional arrays are stored in row-major order: values with the same last index are contiguous. This is different from QB's default.

			%%(qbasic)Dim array(1 TO 2, 6, 3 TO 5) As Single%%			
		The first dimension of the declared array has indices from 1 to 2, the second, 0 to 6, and the third, 3 to 5.  For more information on arrays, please visit the dedicated [[ProPgArrays page]].
		If the values used  with ##**Dim**## to declare the dimensions of an array are all constants, the array will be created ##[[KeyPgOptionstatic Static]]## (unless ##[[KeyPgOptiondynamic Option Dynamic]]## is specified), while using one or more variables to declare the dimensions of an array makes it dynamic, even if ##[[KeyPgOptionstatic Option Static]]## has been issued.
	
		Variables and arrays are stored on the stack by default. The stack space is limited, by default to 1 MB, and exceeding this space results in a crash, with various error messages possible. Defining arrays in the main module and using ##[[KeyPgShared Shared]]## to make them accessible to subs/functions makes FB to place the arrays in main memory, allowing them to be up to 2GB. Using the //-t  stack_size_in_kbytes// in the command line while compiling can help too, if you need to store more than 1 MB on the stack. 
		
		Arrays can be declared as resizable (dynamic) in several ways: Using ##**Dim**## with an empty set of indexes (##**Dim** x()##), using ##**Dim**## with indexes that are variables or using the keyword ##[[KeyPgRedim Redim]]##, or declaring it past the metacommand ##[[KeyPgMetaDynamic $Dynamic]]##. Dynamic arrays can't use initializers.
		Arrays declared with ##**Dim**## and constant indexes and not preceeded by ##[[KeyPgOptiondynamic Option Dynamic]]## are static (not resizable at runtime) and can use initializers.
	            
	**Variable Initializers**
		Arrays, variables, strings, and UDTs are initialized to zero or null strings by default when they are created.
		To avoid the overhead of initializing the variables the ##**Any**## clause can be used with ##**Dim**## to tell the compiler to only preserve the place for the variable but don't initialize it, so the variable will contain garbage. In this case  the programmer should not make assumptions about the initial values.
		Static arrays, variables, zstrings and UDTs may be given a value at the time of their declaration using ##**Dim**##, with the syntax shown above.  Please note the important differences between initializing different types. Arrays, variables and UDT are initialized as they would in a normal assignment, using an equals sign.  The ##=>## sign can be used with fixed length strings to avoid the declaration ressembling an expression.
		 Array values are given in comma-delimited values enclosed by curly brackets, and UDT values are given in comma delimited values enclosed by parenthesis.
		
		These methods of initializing variables can be nested within one another for complex assignments.  For instance, to initialize a multidimensional array:

			%%(qbasic)Dim array(1 to 2, 1 to 5) as Integer => {{1, 2, 3, 4, 5}, {1, 2, 3, 4, 5}}%%			
		In this declaration, the values for the left-most dimension are given as 5-index arrays.  Nesting allows for arrays of any dimension to be initialized.

		UDTs and arrays can be nested within each other as well.  For instance, the following code declares and initializes an array of UDTs
			
			%%(qbasic)Type mytype
	var1 As Double
	var2 As Integer
End Type
		
Dim myvar(0 To 2) As mytype => {(1.0, 1), (2.0, 2)}%%		
		For module-level, static, or global variables, initialized values must be constant expressions.  FreeBASIC will report a compile-time error if otherwise.
		
		Note: Initializing UDT's with strings is not supported at this time.
		
{{fbdoc item="ex"}}
%%(qbasic)
Dim a As Byte
Dim b As Short
Dim c As Integer
Dim d As LongInt
Dim au As UByte
Dim bu As UShort
Dim cu As UInteger
Dim du As ULongInt
Dim e As Single
Dim f As Double
Dim g As Integer Ptr
Dim h As Byte Ptr
Dim s1 As String * 10   '' fixed string
Dim s2 As String        '' variable length string
Dim s3 As ZString Ptr   '' zstring


s1 = "Hello World!"
s2 = "Hello World from FreeBasic!"
s3 = Allocate( Len( s2 ) + 1 )
*s3 = s2

Print "Byte: "; Len(a)
Print "Short: "; Len(b)
Print "Integer: "; Len(c)
Print "Longint: "; Len(d)
Print "UByte: "; Len(au)
Print "UShort: "; Len(bu)
Print "UInteger: "; Len(cu)
Print "ULongint: "; Len(du)
Print "Single: "; Len(e)
Print "Double: "; Len(f)
Print "Integer Pointer: "; Len(g)
Print "Byte Pointer: "; Len(h)
Print "Fixed String: "; Len(s1)
Print "Variable String: "; Len(s2)
Print "ZString: "; Len(*s3)

Deallocate(s3)

Sleep
%%
{{fbdoc item="lang"}}
	- In //-lang qb// the [[ProPgVariableScope scope]] of the variables is a function or a module if defined with ##**Dim Shared**##.
	- In //-lang fb// and //-lang deprecated// the block statements (##[[KeyPgFornext For..Next]]##, ##[[KeyPgWhilewend While..Wend]]##, ##[[KeyPgDoloop Do..Loop]]##, ##[[KeyPgScope Scope..End Scope]]##) define local [[ProPgVariableScope scopes]], variables ##**Dim**##med  within these blocks are not visible outside them.
	- In //-lang fb//, ##**Option**## statements (e.g. ##[[KeyPgOptionbase Option Base]]##, ##[[KeyPgOptiondynamic Option Dynamic]]##), metacommands(e.g. ##[[KeyPgMetaStatic $Static]]##) and ##**Def""###""**## statements (e.g. ##[[KeyPgDefint DefInt]]##) are not allowed.

{{fbdoc item="diff"}}
	- Variable Initializers are new to FreeBASIC
	- The alternate syntax ##Dim As DataType //symbolname//, [...]## is new to FreeBASIC
	- Multidimensional arrays are stored in row-major order in FB, they were stored in column-major order in QB by default. Row major order: values with the same last index are contiguous. Column-major order: values with the same first index are contiguous.
	- Dynamic arrays up to 2GB in size are possible in FB. In QB static arrays were limited to 64KB and to the DOS memory available (several 100 KB at best) if made dynamic and ///AH// was used.

	      
{{fbdoc item="see"}}
	- ##[[KeyPgCommon Common]]##
	- ##[[KeyPgErase Erase]]##
	- ##[[KeyPgExtern Extern]]##
	- ##[[KeyPgLbound LBound]]##
	- ##[[KeyPgRedim Redim]]##
	- ##[[KeyPgPreserve Preserve]]##
	- ##[[KeyPgShared Shared]]##
	- ##[[KeyPgStatic Static]]##
	- ##[[KeyPgUbound UBound]]##
	- ##[[KeyPgVar Var]]##

{{fbdoc item="back" value="CatPgVariables|Variable Declarations"}}